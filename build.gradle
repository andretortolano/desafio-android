import deps.AndroidTestDeps
import deps.Deps
import deps.TestDeps

buildscript {
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$Versions.kotlin"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 30

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 30
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8.toString()
                }

                buildFeatures {
                    viewBinding true
                }
            }

            dependencies {

                implementation(
                        Deps.kotlin,
                        Deps.android,
                        Deps.circleImageView,
                        Deps.retrofit,
                        Deps.lifecycle,
                        Deps.koin,
                        Deps.timber
                )

                testImplementation(
                        TestDeps.junit,
                        TestDeps.truth,
                        TestDeps.mockk,
                        TestDeps.coroutines,
                        TestDeps.androidCore
                )

                androidTestImplementation(
                        AndroidTestDeps.runner,
                        AndroidTestDeps.espresso
                )
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
